--
--  Copyright (c) 2018, German Rivera
--  All rights reserved.
--
--  Redistribution and use in source and binary forms, with or without
--  modification, are permitted provided that the following conditions are met:
--
--  * Redistributions of source code must retain the above copyright notice,
--    this list of conditions and the following disclaimer.
--
--  * Redistributions in binary form must reproduce the above copyright notice,
--    this list of conditions and the following disclaimer in the documentation
--    and/or other materials provided with the distribution.
--
--  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
--  AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
--  IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
--  ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
--  LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
--  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
--  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
--  INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
--  CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
--  ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
--  POSSIBILITY OF SUCH DAMAGE.
--

with Watchdog_Timer;
with Interfaces;
with System.Machine_Code;
with System.Storage_Elements;
with Kinetis_K64F;
with Microcontroller.Arch_Specific;
with Microcontroller.Clocks;
with Reset_Counter;
with Low_Level_Debug;

package body Startup is
   use Interfaces;
   use System.Machine_Code;
   use System.Storage_Elements;
   use Kinetis_K64F;

   type Words_Array_Type is array (Integer_Address range <>) of Unsigned_32;

   procedure Gnat_Generated_Main with Import,
				      Convention => C,
				      External_Name => "main";
   --
   -- main generated by gnatbind
   --

   procedure Enable_FPU;
   --
   --  Enable FPU hardware
   --

   procedure Clear_BSS_Section;
   --
   --  Clear BSS section in SRAM
   --  (C global and static non-initialized variables)
   --

   procedure Copy_Data_Section;
   --
   --  Copy data section from flash to SRAM
   --  (C global and static initialized variables)
   --

   procedure Unexpected_Interrupt_Handler
      with Export, Convention => C;


   --
   --  Linker-script symbols defined in memory_layout.ld
   --

   --  Start address of the .data section in SRAM
   Data_Start : Unsigned_32;
   pragma Import (Asm, Data_Start, "__data_start");

   --  Size in 32-bit words of the .data section
   Data_Words : constant Unsigned_32;
   pragma Import (Asm, Data_Words, "__data_words");

   --  Start address of the .data section in flash
   Data_Load : constant Unsigned_32;
   pragma Import (Asm, Data_Load, "__data_load");

   --  Start address of the .bss section in SRAM
   BSS_Start : Unsigned_32;
   pragma Import (Asm, BSS_Start, "__bss_start");

   --  Size in 32-bit words of the .bss section
   BSS_Words : constant Unsigned_32;
   pragma Import (Asm, BSS_Words, "__bss_words");

   --  End address of the main stack
   Main_Stack_End : constant Unsigned_32;
   pragma Import (Asm, Main_Stack_End, "__stack_end");

   Flash_Config : constant Words_Array_Type (1 .. 4) :=
          (16#FFFFFFFF#,
	   16#FFFFFFFF#,
	   16#FFFFFFFF#,
           16#FFFFFFFE#) with Linker_Section => ".FlashConfig",
                              Unreferenced;

   --
   -- Base interrupt vector number for external IRQs
   --
   IRQ_Vector_Base : constant := 16;

   Last_External_Interrupt : constant :=
     External_Interrupt_Type'Pos (External_Interrupt_Type'Last);

   --
   --  Interrupt vector table in flash
   --
   Interrupt_Vector_Table : constant
     array (0 .. IRQ_Vector_Base + Last_External_Interrupt) of System.Address :=
      (0 =>
	 Main_Stack_End'Address,
       --  Synchronous exceptions and internal interrupts:
       1 =>
	 Reset_Handler'Address,
       2 .. 15 =>
	 Unexpected_Interrupt_Handler'Address,
       --  External interrupts:
       IRQ_Vector_Base + External_Interrupt_Type'Pos(DMA0_IRQ) ..
       IRQ_Vector_Base + External_Interrupt_Type'Pos(ENET_Error_IRQ) =>
         Unexpected_Interrupt_Handler'Address)
      with Linker_Section => ".vectors",
           Unreferenced;

   -----------------------
   -- Clear_BSS_Section --
   -----------------------

   procedure Clear_BSS_Section is
      Num_BSS_Words : constant Integer_Address :=
        To_Integer (BSS_Words'Address);
      BSS_Section : Words_Array_Type (1 .. Num_BSS_Words) with
        Address => BSS_Start'Address;
   begin
      BSS_Section := (others => 0);
   end Clear_BSS_Section;

   -----------------------
   -- Copy_Data_Section --
   -----------------------

   procedure Copy_Data_Section is
      Num_Data_Words : constant Integer_Address :=
        To_Integer (Data_Words'Address);
      Data_Section : Words_Array_Type (1 .. Num_Data_Words) with
	 Address => Data_Start'Address;
      Data_Section_Initializers : Words_Array_Type (1 .. Num_Data_Words) with
	 Address => Data_Load'Address;
   begin
      Data_Section := Data_Section_Initializers;
   end Copy_Data_Section;

   ----------------
   -- Enable_FPU --
   ----------------

   procedure Enable_FPU is
      FPU_Control_Reg : Unsigned_32 with
        Address => System'To_Address (16#E000ED88#);
      Reg_Value : Unsigned_32;
   begin
      Reg_Value := FPU_Control_Reg;
      Reg_Value := Reg_Value or 16#F00000#;
      FPU_Control_Reg := Reg_Value;

      --  Wait for store to complete and reset pipeline with FPU enabled:
      Asm ("dsb" & ASCII.LF &
           "isb" & ASCII.LF,
           Clobber => "memory",
           Volatile => True);
   end Enable_FPU;

   -------------------
   -- Reset_Handler --
   -------------------

   procedure Reset_Handler is
   begin
      Microcontroller.Arch_Specific.Disable_Cpu_Interrupts;

      --
      --  Watchdog timer needs to initialized as soon as possible, to prevent
      --  it from firing, in case it is enabled by default:
      --
      Watchdog_Timer.Initialize;

      Low_Level_Debug.Initialize_Rgb_Led;
      Low_Level_Debug.Set_Rgb_Led(Red_On => True);

      Microcontroller.Clocks.Initialize;
      Low_Level_Debug.Set_Rgb_Led(Green_On => True);

      --
      --  In case C code is invoked from Ada, C global variables
      --  need to be initialized in RAM:
      --
      Copy_Data_Section;
      Clear_BSS_Section;

      Reset_Counter.Update;
      Enable_FPU;
      Low_Level_Debug.Initialize_Uart;
      --???Low_Level_Debug.Set_Rgb_Led; --  off

      Microcontroller.Arch_Specific.Interrupt_Handling_Init;
      Microcontroller.Arch_Specific.Enable_Cpu_Interrupts;

      --
      --  Call main() generated by gnatbind, which will do the elaboration
      --  of Ada library-level packages and then invoke the main Ada subprogram
      --
      --  NOTE: Before calling 'Gnat_Generated_Main' only
      --  "No_Elaboration_Code_All" packages can be invoked.
      --
      Gnat_Generated_Main;

      --
      --  We should not return here
      --
      loop
	 null;
      end loop;
   end Reset_Handler;

   -------------------
   -- Reset_Handler --
   -------------------

   procedure Unexpected_Interrupt_Handler is
   begin
      loop
	 null;
      end loop;
   end Unexpected_Interrupt_Handler;
end Startup;
