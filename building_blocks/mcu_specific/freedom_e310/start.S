#define MSTATUS_FS          0x00006000

        .section .start
        .globl _start
        .type _start,@function

_start:
	.cfi_startproc
	.cfi_undefined ra

        la gp, __global_pointer
        la sp, __stack_end

        /* Load data section (needed for C code) */
        la a0, __data_load
        la a1, __data_start
        la a2, __data_end
        bgeu a1, a2, 2f
1:
        lw t0, (a0)
        sw t0, (a1)
        addi a0, a0, 4
        addi a1, a1, 4
        bltu a1, a2, 1b
2:

        /* Clear bss section (needed for C code) */
        la a0, __bss_start
        la a1, __bss_end
        bgeu a0, a1, 2f
1:
        sw zero, (a0)
        addi a0, a0, 4
        bltu a0, a1, 1b
2:

	/* Enable FPU */
#if 0
	li t0, MSTATUS_FS
	csrs mstatus, t0
	csrr t1, mstatus
	and t1, t1, t0
	beqz t1, 1f
	fssr zero
1:
#endif
        call microcontroller__mcu_specific__pre_elaboration_init
        call main    /* main generated by gnatbind */
        call _exit

2:      j 2b
	.cfi_endproc

        .globl _exit
        .type _exit,@function
        .globl _abort
        .type abort,@function
abort:
_exit:
        ebreak
        j _exit
